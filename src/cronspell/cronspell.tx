DateMaths:
  anchor=Anchor? formula=Formula?
;

Formula:
    DateMath | DateMatSet
;

DateMatSet:
    SetOpening set*=DateMath[','] SetClosing
;

SetOpening:
    '{'
;

SetClosing:
    '}'
;


DateMath:
    date_math_term*=DateMathTerm
;

Anchor:
    tznow=TimezoneNow | NaiveNow | isodate=ISODate
;

ISODate:
    /\d+\S+/
;

DateMathTerm:
    statement=Statement
;

NaiveNow:
  'now'
;

TimezoneNow:
  NaiveNow '[' tz=/\s*([^\]]+)\s*/ ']'
;


Statement:
    Floor |  Plus | Minus | CwModulo | YModulo | MonthModulo
;


CwModulo:
   CalendarWeekModuloMarker value=INT?
;

YModulo:
   YearModuloMarker value=INT?
;

MonthModulo:
   MonthModuloMarker value=INT?
;

CalendarWeekModuloMarker:
    /[%,@]\s*\b(CW|Cw|cw)/
;

YearModuloMarker:
    /[%,@]\s*\b(Y|y)(ears?)?/
;

MonthModuloMarker:
    /[%,@]\s*\b(m)(onths?)?/
;

Floor:
  '/' res=Clampable
;

Plus:
  '+' steps=INT? res=TimeUnitShort
;

Minus:
  '-' steps=INT? res=TimeUnitShort
;

Clampable:
    WeekDay | MonthName | TimeUnitShort | VariableTimeUnit
;

TimeUnitShort:
    W=W | d=d | M=M | H=H | S=S
;

VariableTimeUnit:
    Y=Y | m=m
;

WeekDay:
    Mon=Mon | Tue=Tue | Wed=Wed | Thu=Thu | Fri=Fri | Sat=Sat | Sun=Sun
;


MonthName:
    Jan=Jan | Feb=Feb | Mar=Mar | Apr=Apr | May=May | Jun=Jun | Jul=Jul | Aug=Aug | Sep=Sep | Oct=Oct | Nov=Nov | Dec=Dec
;


Y:
    /([Yy]ears?|Y\b)/
;

m:
    /(?!mon\b)([mM]onths?|m\b)/
;

W:
    /\b([wW]eeks?|W)\b/
;

d:
    /([dD]ays?|d\b)/
;

M:
    /([mM]inutes?|M\b|[mM]in\b)/
;

H:
    /([hH]ours?|H\b|[Hh]rs)/
;

S:
    /(?!sat$)([sS]econds?|S\b|[sS]ec\b)/
;

Mon:
    /\bmon\b/
;

Tue:
    /\btue\b/
;

Wed:
    /\bwed\b/
;

Thu:
    /\bthu\b/
;

Fri:
    /\bfri\b/
;

Sat:
    /\bsat\b/
;

Sun:
    /\bsun\b/
;

// ============ month names as in calendar.month_abbr ============

Jan:
    /\b[jJ]an\b/
;


Feb:
    /\b[fF]eb\b/
;


Mar:
    /\b[mM]ar\b/
;


Apr:
    /\b[aA]pr\b/
;


May:
    /\b[mM]ay\b/
;


Jun:
    /\b[jJ]un\b/
;


Jul:
    /\b[jJ]ul\b/
;


Aug:
    /\b[aA]ug\b/
;


Sep:
    /\b[sS]ep\b/
;


Oct:
    /\b[oO]ct\b/
;


Nov:
    /\b[nN]ov\b/
;


Dec:
    /\b[dD]ec\b/
;

// //////////////////////////////////////////////
Comment:
    /\/\*(.|\n)*?\*\// | /\/\/.*?$/
;
