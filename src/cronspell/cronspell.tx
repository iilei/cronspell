DateMaths:
  anchor=Anchor? formula=Formula?
;

Formula:
    DateMath | DateMatSet
;

DateMatSet:
    SetOpening set*=DateMath[','] SetClosing
;

SetOpening:
    '{'
;

SetClosing:
    '}'
;


DateMath:
    date_math_term*=DateMathTerm
;

Anchor:
    tznow=TimezoneNow | NaiveNow | isodate=ISODate
;

ISODate:
    /\d+\S+/
;

DateMathTerm:
    statement=Statement
;

NaiveNow:
  'now'
;

TimezoneNow:
  NaiveNow '[' tz=/\s*([^\]]+)\s*/ ']'
;


Statement:
    Floor |  Plus | Minus | CwModulo | YModulo | MonthModulo
;


CwModulo:
   CalendarWeekModuloMarker value=INT?
;

YModulo:
   YearModuloMarker value=INT?
;

MonthModulo:
   MonthModuloMarker value=INT?
;

CalendarWeekModuloMarker:
    /[%,@]\s*\b(CW|Cw|cw)/
;

YearModuloMarker:
    /[%,@]\s*\b(Y|y)(ears?)?/
;

MonthModuloMarker:
    /[%,@]\s*\b(m)(onths?)?/
;

Floor:
  '/' res=Clampable
;

Plus:
  '+' steps=INT? res=TimeUnitShort
;

Minus:
  '-' steps=INT? res=TimeUnitShort
;

Clampable:
    WeekDay | MonthName | TimeUnitShort | VariableTimeUnit
;

TimeUnitShort:
    W=W | d=d | M=M | H=H | S=S
;

VariableTimeUnit:
    Y=Y | m=m
;

WeekDay:
    Mon=Mon | Tue=Tue | Wed=Wed | Thu=Thu | Fri=Fri | Sat=Sat | Sun=Sun
;


MonthName:
    Jan=Jan | Feb=Feb | Mar=Mar | Apr=Apr | May=May | Jun=Jun | Jul=Jul | Aug=Aug | Sep=Sep | Oct=Oct | Nov=Nov | Dec=Dec
;


Y:
    /([Yy]ears?|Y\b)/
;

m:
    /(?!mon\b)([mM]onths?|m\b)/
;

W:
    /\b([wW]eeks?|W)\b/
;

d:
    /([dD]ays?|d\b)/
;

M:
    /([mM]inutes?|M\b|[mM]in\b)/
;

H:
    /([hH]ours?|H\b|[Hh]rs)/
;

S:
    /(?!sat$)([sS]econds?|S\b|[sS]ec\b)/
;

Mon:
    /\b[mM]on(?:day)?\b/
;

Tue:
    /\b[tT]ue(?:sday)?\b/
;

Wed:
    /\b[wW]ed(?:nesday)?\b/
;

Thu:
    /\b[tT]hu(?:esday)?\b/
;

Fri:
    /\b[fF]ri(?:day)?\b/
;

Sat:
    /\b[sS]at(?:urday)?\b/
;

Sun:
    /\b[sS]un(?:day)?\b/
;

// ============ month names as in calendar.month_abbr ============

Jan:
    /\b[jJ]an(?:uary)?\b/
;


Feb:
    /\b[fF]eb(?:ruary)?\b/
;


Mar:
    /\b[mM]ar(?:ch)?\b/
;


Apr:
    /\b[aA]pr(?:il)?\b/
;


May:
    /\b[mM]ay\b/
;


Jun:
    /\b[jJ]une?\b/
;


Jul:
    /\b[jJ]uly?\b/
;


Aug:
    /\b[aA]ug(?:ust)?\b/
;


Sep:
    /\b[sS]ep(?:tember)?\b/
;


Oct:
    /\b[oO]ct(?:ober)?\b/
;


Nov:
    /\b[nN]ov(?:ember)?\b/
;


Dec:
    /\b[dD]ec(?:ember)?\b/
;

// //////////////////////////////////////////////
Comment:
    /\/\*(.|\n)*?\*\// | /\/\/.*?$/
;
