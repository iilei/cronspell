
DateMath:
    anchor=Anchor? date_math_term*=DateMathTerm
;

Anchor:
    tznow=TimezoneNow | NaiveNow | isodate=ISODate
;

ISODate:
    /\d+\S+/
;

DateMathTerm:
    statement=Statement
;

NaiveNow:
  'now'
;

TimezoneNow:
  NaiveNow '[' tz=/\s*([^\]]+)\s*/ ']'
;


Statement:
    Floor |  Plus | Minus
;

Floor:
  '/' res=Clampable
;

Plus:
  '+' steps=INT? res=TimeUnitShort
;

Minus:
  '-' steps=INT? res=TimeUnitShort
;

Clampable:
    WeekDay | TimeUnitShort
;


TimeUnitShort:
    Y=Y | m=m | W=W | d=d | M=M | H=H | S=S

;

WeekDay:
    Mon=Mon | Tue=Tue | Wed=Wed | Thu=Thu | Fri=Fri | Sat=Sat | Sun=Sun
;

Y:
    /([Yy]ears?|Y\b)/
;

m:
    /(?!mon\b)([mM]onths?|m\b)/
;

W:
    /([wW]eeks?|W\b)/
;

d:
    /([dD]ays?|d\b)/
;

M:
    /([mM]inutes?|M\b)/
;

H:
    /([hH]ours?|H\b)/
;

S:
    /(?!sat$)([sS]econds?|S\b)/
;

Mon:
    /\bmon\b/
;

Tue:
    /\btue\b/
;

Wed:
    /\bwed\b/
;

Thu:
    /\bthu\b/
;

Fri:
    /\bfri\b/
;

Sat:
    /\bsat\b/
;

Sun:
    /\bsun\b/
;

Comment:
    /\/\*(.|\n)*?\*\// | /\/\/.*?$/
;
